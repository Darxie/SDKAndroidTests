plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 34
    ndkVersion "21.1.6352462"
    defaultConfig {
        applicationId "com.sygic.sdk.test.android"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // use your own SDK client ID
        buildConfigField "String", "SYGIC_SDK_CLIENT_ID", getSdkClientId()
        buildConfigField "String", "LICENSE_KEY", getSdkLicenseKey()
        buildConfigField "String", "SYGIC_SDK_CLIENT_ID_HERE", getSdkClientIdForHereMaps()
        buildConfigField "String", "LICENSE_KEY_HERE", getSdkLicenseKeyForHereMaps()

        if (gradle.wantsToBuildSdkFromSource) {
            missingDimensionStrategy 'build', 'development'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17

        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/main/assets']
        }
    }
    namespace 'cz.feldis.sdkandroidtests'
}

def getSdkVersion() {
    Properties properties = new Properties()
    def localProperties = new File(project.rootDir, 'local.properties')
    if (!localProperties.exists())
        throw new GradleScriptException("File local.properties doesn\'t exist")
    properties.load(localProperties.newDataInputStream())
    String version = properties.getProperty('sygic.sdk.version')
    if (version == null)
        throw new GradleScriptException("Property sygic.sdk.version is not set")

    return version
}

def getSdkClientId() {
    def sdkClientId = ""

    def localProperties = project.rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(localProperties.newDataInputStream())
        sdkClientId = properties.getProperty('sygic.sdk.client.id', "")
    }

    if (sdkClientId.isEmpty()) {
        logger.error('Missing Client ID for Sygic Maps SDK! Define one in local.properties as sygic.sdk.client.id="YOUR.CLIENT.ID"')
    }

    return sdkClientId
}

def getSdkLicenseKey() {
    def sdkLicenseKey = ""

    def localProperties = project.rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(localProperties.newDataInputStream())
        sdkLicenseKey = properties.getProperty('license.key', "")
    }

    if (sdkLicenseKey.isEmpty()) {
        logger.error('Missing License Key for Sygic Maps SDK! Define one in local.properties as license.key="jwt"')
    }

    return sdkLicenseKey
}

def getSdkClientIdForHereMaps() {
    def sdkClientId = ""

    def localProperties = project.rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(localProperties.newDataInputStream())
        sdkClientId = properties.getProperty('sygic.sdk.client.id.here', "")
    }

    if (sdkClientId.isEmpty()) {
        logger.error('Missing Client ID for Sygic Maps SDK! Define one in local.properties as sygic.sdk.client.id="YOUR.CLIENT.ID"')
    }

    return sdkClientId
}

def getSdkLicenseKeyForHereMaps() {
    def sdkClientId = ""

    def localProperties = project.rootProject.file('local.properties')
    if (localProperties.exists()) {
        Properties properties = new Properties()
        properties.load(localProperties.newDataInputStream())
        sdkClientId = properties.getProperty('license.key.here', "")
    }

    if (sdkClientId.isEmpty()) {
        logger.error('Missing License Key for Sygic Maps SDK! Define one in local.properties as license.key="jwt"')
    }

    return sdkClientId
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1"

    implementation("com.sygic.lib.auth:sygicauth-android:1.1.0") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.jetbrains.kotlinx'
    }

    if (!gradle.wantsToBuildSdkFromSource()) {
        // use aar from maven
        api("com.sygic.sdk:maps-android:${getSdkVersion()}@aar") {
            exclude group: 'com.google.android.gms'
            exclude group: 'androidx.core', module: 'core-ktx'
            transitive true
        }
    } else {
        // compile sdk yourself (with correctly included sdk module of course)
        api project(':sdk')
    }

    implementation 'com.google.android.material:material:1.12.0'
    androidTestImplementation ('org.mockito:mockito-android:5.12.0')
    androidTestImplementation ('org.mockito.kotlin:mockito-kotlin:5.4.0')
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1"
}